plugins {
    // Apply the java plugin to add support for Java
    id "java"

    // Apply the application plugin to add support for building a CLI application
    // You can run your app via task "run": ./gradlew run
    id "application"

    /*
     * Adds tasks to export a runnable jar.
     * In order to create it, launch the "shadowJar" task.
     * The runnable jar will be found in build/libs/projectname-all.jar
     */
    id("com.github.johnrengelman.shadow") version "6.1.0"
}

repositories {
    mavenCentral()
}

def javaFXModules = [
    "base",
    "controls",
    "fxml",
    "swing",
    "graphics",
    "media"
]

def supportedPlatforms = ["linux", "mac", "win"] // All required for OOP

def javaFxVersion = "15.0.1"

def jUnitVersion = "5.7.1"

dependencies {

    for (platform in supportedPlatforms) {
        for (module in javaFXModules) {
            implementation("org.openjfx:javafx-$module:$javaFxVersion:$platform")
        }
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:$jUnitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jUnitVersion")
    testCompile("org.testfx:testfx-junit5:4.0.16-alpha")
    
    implementation("com.google.guava:guava:30.1-jre")
    implementation("com.google.code.gson:gson:2.8.6")

}

test {
    useJUnitPlatform()
}


application {
    mainClassName = "jhaturanga.Launcher"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

